// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zxr);
    Not16(in=zxr, out=zxrnot);
    Mux16(a=zxr, b=zxrnot, sel=nx, out=xpost);
    Mux16(a=y, b=false, sel=zy, out=zyr);
    Not16(in=zyr, out=zyrnot);
    Mux16(a=zyr, b=zyrnot, sel=ny, out=ypost);
    Add16(a=xpost, b=ypost, out=addres);
    And16(a=xpost, b=ypost, out=andres);
    Mux16(a=andres, b=addres, sel=f, out=fres);
    Not16(in=fres, out=fresnot);
    Mux16(a=fres, b=fresnot, sel=no, out=out, out[0..7]=outlsb, out[8..15]=outmsb, out[15]=ng);
    Or8Way(in=outlsb, out=or1);
    Or8Way(in=outmsb, out=or2);
    Or(a=or1, b=or2, out=or3);
    Not(in=or3, out=zr);
}
